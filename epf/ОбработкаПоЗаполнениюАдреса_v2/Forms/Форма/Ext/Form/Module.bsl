
&НаКлиенте
Процедура АдресНажатие(Элемент)
	
	ВидАдреса = ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица");
	РедактироватьАдрес(Элемент, ВидАдреса);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьАдрес(Элемент, ВидАдреса)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
	"АдресНажатие_Завершение", 
	ЭтотОбъект, 
	Элемент);
	
	ПараметрыОткрытия = УправлениеКонтактнойИнформациейКлиент.ПараметрыФормыКонтактнойИнформации(ВидАдреса, ЭтотОбъект[Элемент.Имя]);
	ПараметрыОткрытия.Вставить("ТолькоПросмотр", Ложь);
	
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, , ОписаниеОповещения);
	
КонецПроцедуры


&НаКлиенте
Процедура АдресНажатие_Завершение(Результат, Элемент) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Адрес         = Результат.КонтактнаяИнформация;
	Представление = Результат.Представление;
	Элемент.Заголовок       = Представление;
	ЭтотОбъект[Элемент.Имя] = Результат.КонтактнаяИнформация;

	Сохранить();
	
КонецПроцедуры


&НаСервере
Процедура Сохранить() 
	КлючОбъекта  = "ОбработкаВводаАдреса";
	КлючНастроек = "ВводАдреса";
	Настройки  = Новый Структура("Адрес,Представление", Адрес,Представление);
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(КлючОбъекта, КлючНастроек, Настройки,,ИмяПользователя());
	
КонецПроцедуры  



&НаСервере
Процедура Восстановить() 
	СтруктураОбщихПараметров = Неопределено;
	СтруктураОбщихПараметров = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ОбработкаВводаАдреса", "ВводАдреса", , , ИмяПользователя());
	Если ТипЗнч(СтруктураОбщихПараметров) = Тип("Структура") Тогда
		Если СтруктураОбщихПараметров.Свойство("Адрес") Тогда 
			Адрес =  СтруктураОбщихПараметров.Адрес;
			
			Если СтруктураОбщихПараметров.Свойство("Представление") Тогда 
				Представление = СтруктураОбщихПараметров.Представление; 
				Элементы.Адрес.Заголовок = Представление;	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Восстановить();
КонецПроцедуры

